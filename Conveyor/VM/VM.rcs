PROGR VERIFIER
{
	TACT 50;
	CONST AlgorithmDelay 10; /*1 сек*/
	CONST RemovingDelay 10;
	CONST Error 0;
	CONST Norm 1;
	
	/*List of commands from CA (Control Algorithm)*/
	ENUM 
	{
		CA2GUI_PRESENCE_SENSOR_ERROR,
		CA2GUI_COLOR_SENSORS_ERROR
	};

	/*List of commands for VM (Verification Module)*/
	ENUM 
	{
		SCM2VM_TEST_PUT_BOXES_ON_CONVEYOR,
		SCM2VM_TEST_BROKEN_PRESENCE_SENSOR,
		SCM2VM_TEST_BROKEN_COLOR_SENSORS,
		SCM2VM_TEST_COMPLETED,
		SCM2VM_FINISH_VERIFICATION
	};

	/*List of commands for SCM (Script Control Module) from VM (Verification Module)*/ 
	ENUM
	{
		VM2SCM_START_VERIFICATION
	};

	/*List of commands for GUI*/
	ENUM
	{
		VM2GUI_TEST_PASSED_WRONG_PRESENCE_SENSOR,
		VM2GUI_TEST_PASSED_WRONG_COLOR_SENSORS,
		VM2GUI_TEST_FAILED_WRONG_PRESENCE_SENSOR,
		VM2GUI_TEST_FAILED_WRONG_COLOR_SENSORS,
		VM2GUI_TEST_PASSED_GREEN_BOXES_CONTROL,
		VM2GUI_TEST_FAILED_GREEN_BOXES_CONTROL,		
		VM2GUI_TEST_PASSED_RED_BOXES_CONTROL,
		VM2GUI_TEST_FAILED_RED_BOXES_CONTROL,
		VM2GUI_TEST_PASSED_BLUE_BOXES_CONTROL,
		VM2GUI_TEST_FAILED_BLUE_BOXES_CONTROL,
		VM2GUI_TEST_PASSED_MAIN_CONVEYOR_CONTROL,
		VM2GUI_TEST_FAILED_MAIN_CONVEYOR_CONTROL,
		VM2GUI_START_TEST_RANDOM_BOXES,
		VM2GUI_START_TEST_WRONG_PRESENCE_SENSOR,
		VM2GUI_START_TEST_WRONG_COLOR_SENSORS,
		VM2GUI_END_VERIFICATION
	};
	
	FUNCTION INT SendMsgSCMCode(INT);
	FUNCTION INT SendMsgGUICode(INT);
	FUNCTION INT GetNextMsgFromSCM(VOID);
	FUNCTION INT GetMsgCodeFromSCM(VOID);
	FUNCTION INT GetNextMsgFromCA(VOID);
	FUNCTION INT GetMsgCodeFromCA(VOID);

	INPUT  LOG_CA_INPUT_PORT 0 0 8; /*name, base address, add. addr port, 8bit*/
	INPUT  LOG_CA_OUTPUT_PORT 0 1 8; /*name, base address, add. addr port, 8bit*/
	
	PROC Init
	{	
		LOG I_BOX_UNDER_SENSOR = {LOG_CA_INPUT_PORT[1]} FOR ALL;
		LOG I_BOX_RED = {LOG_CA_INPUT_PORT[1]} FOR ALL;
		LOG I_BOX_GREEN = {LOG_CA_INPUT_PORT[1]} FOR ALL;
		
		LOG I_TURN_ON_MAIN_CONVEYOR = {LOG_CA_OUTPUT_PORT[1]} FOR ALL;
		LOG I_TURN_ON_SIDE_CONVEYOR = {LOG_CA_OUTPUT_PORT[1]} FOR ALL;
		LOG I_REMOVE_BOX = {LOG_CA_OUTPUT_PORT[1]} FOR ALL;
		
		STATE Start 
		{	
			SendMsgSCMCode(VM2SCM_START_VERIFICATION); 	
			START PROC ReceiveSCMOutputMsg;
			START PROC ReceiveCAOutputMsg;
			STOP;
		}
	} 
	
	PROC ReceiveCAOutputMsg
	{
		INT CAMsgCode LOCAL;
		LOG PresenceFlag FOR ALL; 
		LOG ColorFlag FOR ALL;

		STATE Init
		{		
			PresenceFlag = Norm;
			ColorFlag = Norm;
			SET NEXT;
		}
		
		STATE Start
		{
			IF (GetNextMsgFromCA()) 
			{
				CAMsgCode = GetMsgCodeFromCA();
				SWITCH (CAMsgCode) 				
				{
					CASE CA2GUI_PRESENCE_SENSOR_ERROR:
						PresenceFlag = Error;
					BREAK;
					CASE CA2GUI_COLOR_SENSORS_ERROR:
						ColorFlag = Error;
					BREAK;
				}
			}
			LOOP;
		}
	}
	
	PROC ReceiveSCMOutputMsg
	{  
		INT SCMsgCode LOCAL;
		
		STATE Start
		{
			/*Read code and event parameter from ring buffer into the structure*/
			IF (GetNextMsgFromSCM()) 
			{
				SCMsgCode = GetMsgCodeFromSCM();
				SWITCH (SCMsgCode) 				
				{			
					CASE SCM2VM_TEST_PUT_BOXES_ON_CONVEYOR:			
						SendMsgGUICode(VM2GUI_START_TEST_RANDOM_BOXES);
						START PROC GreenBoxControl; 
						START PROC RedBoxControl;
						START PROC BlueBoxControl;
						START PROC MainConveyorControl;	
						BREAK;
						
					CASE SCM2VM_TEST_BROKEN_PRESENCE_SENSOR:
						SendMsgGUICode(VM2GUI_START_TEST_WRONG_PRESENCE_SENSOR);
						START PROC PresenceSensorControl;	
					BREAK;
					
					CASE SCM2VM_TEST_BROKEN_COLOR_SENSORS:
						SendMsgGUICode(VM2GUI_START_TEST_WRONG_COLOR_SENSORS);
						START PROC ColorSensorsControl;	
					BREAK;
					
					CASE SCM2VM_TEST_COMPLETED:
						START PROC Terminator; 
						BREAK;
					
					CASE SCM2VM_FINISH_VERIFICATION:
						SendMsgGUICode(VM2GUI_END_VERIFICATION);
						STOP PROC Terminator; 
						STOP;
						BREAK;
				}
			}
			LOOP;
		}
	}
	
	PROC Terminator
	{
		STATE Report
		{	/*GreenBoxControl*/		
			IF(PROC GreenBoxControl SET STATE ACTIVE)
			{
				SendMsgGUICode(VM2GUI_TEST_PASSED_GREEN_BOXES_CONTROL);
			}
			IF (PROC GreenBoxControl SET STATE ERROR)
			{
				SendMsgGUICode(VM2GUI_TEST_FAILED_GREEN_BOXES_CONTROL);
			}

			STOP PROC GreenBoxControl;
			/*RedBoxControl*/
			IF(PROC RedBoxControl SET STATE ACTIVE)
			{
				SendMsgGUICode(VM2GUI_TEST_PASSED_RED_BOXES_CONTROL);
			}
			IF (PROC RedBoxControl SET STATE ERROR)
			{
				SendMsgGUICode(VM2GUI_TEST_FAILED_RED_BOXES_CONTROL);
			}

			STOP PROC RedBoxControl;
			/*BlueBoxControl*/
			IF(PROC BlueBoxControl SET STATE ACTIVE)
			{
				SendMsgGUICode(VM2GUI_TEST_PASSED_BLUE_BOXES_CONTROL);
			}
			IF (PROC BlueBoxControl SET STATE ERROR)
			{
				SendMsgGUICode(VM2GUI_TEST_FAILED_BLUE_BOXES_CONTROL);
			}

			STOP PROC BlueBoxControl;
			/*MainConveyorControl*/
			IF(PROC MainConveyorControl SET STATE ACTIVE)
			{
				SendMsgGUICode(VM2GUI_TEST_PASSED_MAIN_CONVEYOR_CONTROL); 
			}
			IF (PROC MainConveyorControl SET STATE ERROR)
			{
				SendMsgGUICode(VM2GUI_TEST_FAILED_MAIN_CONVEYOR_CONTROL);
			}

			STOP PROC MainConveyorControl;
			/*PresenceSensorControl*/
			IF(PROC PresenceSensorControl SET STATE ACTIVE)
			{
				SendMsgGUICode(VM2GUI_TEST_FAILED_WRONG_PRESENCE_SENSOR); 
			}
			IF (PROC PresenceSensorControl SET STATE ERROR)
			{
				SendMsgGUICode(VM2GUI_TEST_PASSED_WRONG_PRESENCE_SENSOR);
			}

			STOP PROC PresenceSensorControl;
			/*ColorSensorsControl*/
			IF(PROC ColorSensorsControl SET STATE ACTIVE)
			{
				SendMsgGUICode(VM2GUI_TEST_FAILED_WRONG_COLOR_SENSORS); 
			}
			IF (PROC ColorSensorsControl SET STATE ERROR)
			{
				SendMsgGUICode(VM2GUI_TEST_PASSED_WRONG_COLOR_SENSORS);
			}

			STOP PROC ColorSensorsControl;
			STOP;
		}	
	}
/*Checking requirement: The operator should be informed*/
/*in case of breakdown of the input signal sensors*/
PROC PresenceSensorControl
{
	FROM PROC ReceiveCAOutputMsg PresenceFlag;
	
	STATE Control
	{
		IF(PresenceFlag == Error)
			ERROR;
		LOOP;
	}
}
/*Checking requirement: The operator should be informed*/
/*in case of breakdown of the input signal sensors*/
PROC ColorSensorsControl
{
	FROM PROC ReceiveCAOutputMsg ColorFlag;
	
	STATE Control
	{
		IF(ColorFlag == Error)
			ERROR;
		LOOP;
	}
}
	
/*Проверка требования: If a green box appears under the sensor,*/
/*the side conveyor will be turned on for F cycles*/
PROC GreenBoxControl
{
	FROM PROC Init I_BOX_UNDER_SENSOR, I_BOX_GREEN, 
	I_TURN_ON_SIDE_CONVEYOR, I_REMOVE_BOX;	
	
	STATE WaitingOfGreenBox /*Waiting for the green box*/
	{
		IF(I_BOX_UNDER_SENSOR && I_BOX_GREEN)
			SET STATE WaitingOnLaunch;
	}
	
	STATE WaitingOnLaunch /*Box under the sensor -> conveyor is on*/
	{
		IF(I_TURN_ON_SIDE_CONVEYOR)
			SET NEXT;
		TIMEOUT AlgorithmDelay ERROR;
	}
	
	STATE StabilityControl /*The conveyor does not turn off while the box under the sensor*/
	{
		IF(!I_BOX_UNDER_SENSOR)
			SET STATE WaitingOfGreenBox;	
		ELSE IF(!I_TURN_ON_SIDE_CONVEYOR || I_REMOVE_BOX) ERROR;	
	}
}

/*Checking the requirement: If a red box appears under the sensor,*/
/*the side conveyor shuts down for F cycles*/
PROC RedBoxControl
{
	FROM PROC Init I_BOX_UNDER_SENSOR, I_BOX_RED, 
	I_TURN_ON_SIDE_CONVEYOR, I_TURN_ON_MAIN_CONVEYOR, I_REMOVE_BOX;	
	
	STATE WaitingOfRedBox /*Waiting for the appearance of the red box*/
	{
		IF(I_BOX_UNDER_SENSOR && I_BOX_RED)
			SET STATE WaitingOnLaunch;
	}
	
	STATE WaitingOnLaunch /*Box under the sensor -> conveyor is on*/
	{
		IF(!I_TURN_ON_SIDE_CONVEYOR && I_TURN_ON_MAIN_CONVEYOR)
			SET NEXT;
		TIMEOUT AlgorithmDelay ERROR;
	}
	
	STATE StabilityControl /*The conveyor does not turn off while the box under the sensor*/
	{		
		IF(!I_BOX_UNDER_SENSOR)
			SET STATE WaitingOfRedBox;	
		ELSE IF(I_TURN_ON_SIDE_CONVEYOR || !I_TURN_ON_MAIN_CONVEYOR || I_REMOVE_BOX) ERROR;	
	}
}

/*Checking the requirement: If a blue box appears under the sensor, then*/ 
/*both conveyors turn off during F cycles, and the box will be removed during M cycles*/
PROC BlueBoxControl
{
	FROM PROC Init I_REMOVE_BOX, I_BOX_RED, I_BOX_GREEN, I_BOX_UNDER_SENSOR, I_TURN_ON_SIDE_CONVEYOR, I_TURN_ON_MAIN_CONVEYOR;	
	
	STATE WaitingOfBlueBox /*Waiting for the blue box*/
	{
		IF(I_BOX_UNDER_SENSOR && !I_BOX_RED && !I_BOX_GREEN)
			SET STATE WaitingForShutdown;
		ELSE IF(I_REMOVE_BOX) ERROR;
	}
		
	STATE WaitingForShutdown /*Box under the sensor -> both conveyors are off*/
	{
		IF(!I_TURN_ON_SIDE_CONVEYOR && !I_TURN_ON_MAIN_CONVEYOR)
			SET NEXT;
		TIMEOUT AlgorithmDelay ERROR;
	}
	
	STATE RemovingControl
	{
		IF((I_REMOVE_BOX && !I_TURN_ON_SIDE_CONVEYOR && !I_TURN_ON_MAIN_CONVEYOR) || !I_BOX_UNDER_SENSOR) 
			SET STATE WaitingOfBlueBox;	
		TIMEOUT RemovingDelay ERROR;
	}
}

/*Checking requirements: The main conveyor shuts down no longer than M + 2 * F cycles*/
PROC MainConveyorControl 
{
	FROM PROC Init I_TURN_ON_MAIN_CONVEYOR;	
	INT delay LOCAL;
	
	STATE Start
	{
		delay = 2*AlgorithmDelay + RemovingDelay;
		SET NEXT;
	}
	
	STATE WaitingForShutdown
	{
		IF(!I_TURN_ON_MAIN_CONVEYOR)
			SET NEXT;
	}
	
	STATE TurnOffControl
	{
		TIMEOUT delay
		{
			IF(I_TURN_ON_MAIN_CONVEYOR)
				SET STATE WaitingForShutdown;
			ELSE ERROR;
		}
	}
}
}













